apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

// android配置
def configs = rootProject.ext.android
// 依赖库配置
def libs = rootProject.ext.dependencies
// maven配置
def mavenConfig = rootProject.ext.maven
android {
    compileSdkVersion configs.compileSdkVersion

    defaultConfig {
        minSdkVersion configs.libMinSdkVersion
        targetSdkVersion configs.targetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

}

dependencies {
    compileOnly libs.recyclerview
    compileOnly project(libs.MTRVA_File)
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from 'src/main/java/develop'
    from 'src/main/java/develop'
}

task javadocs(type: Javadoc) {
    source = 'src/main/java/null'
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    excludes = ['**/*.kt']
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = mavenConfig.groupId
version = mavenConfig.publishVersion

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
}

publishing {
    publications {
        release(MavenPublication) {
            groupId mavenConfig.groupId
            artifactId mavenConfig.componentArtifactId
            version mavenConfig.publishVersion
            if (project.plugins.findPlugin("com.android.library")) {
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            } else {
                artifact("$buildDir/libs/${project.getName()}-${version}.jar")
            }

            artifact androidSourcesJar
            artifact javadocJar

            pom {
                name = mavenConfig.componentArtifactId
                description = mavenConfig.desc
                url = mavenConfig.website
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'crazysunj'
                        name = 'crazysunj'
                        email = '387953660@qq.com'
                    }
                }
                scm {
                    connection = mavenConfig.connection
                    developerConnection = mavenConfig.connection
                    url = mavenConfig.website
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "mavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
